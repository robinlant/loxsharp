program        -> declaration* EOF ;

declaration    -> varDecl
               | funDecl
               | statement ;

varDecl        -> "var" IDENTIFIER ( "=" expression )? ";" ;
funDecl        -> "fun" function ;
function       -> IDENTIFIER "(" parameters? ")" block ;

statement      -> exprStmt
               | printStmt
               | block
               | ifStmt
               | whileStmt
               | forStmt
               | breakStmt
               | returnStmt ;

exprStmt       -> expression ";" ;
printStmt      -> "print" expression ";" ;
block          -> "{" declaration "}" ;
ifStmt         -> "if" "(" expression ")" statement
               ( "else" statement )? ;
whileStmt      -> "while" "(" expression ")" statement ;
forStmt        -> "for" "(" ( varDecl | exprStmt ";" )
               expression? ";"
               expression? ")" statement ;
breakStmt      -> "break" ";" ;
returnStmt     -> "return" expression? ";" ;


expression     -> assignment ;
assignment     -> IDENTIFIER "=" assignment | comma ;
comma          -> conditional ( "," conditional)* ;
conditional    -> logic_or ("?" conditional ":" conditional)* ;
logic_or       -> logic_and ( "or" logic_and )* ;
logic_and      -> equality ( "and" equality )* ;
equality       -> comparison ( ( "!=" | "==" ) comparison )* ;
comparison     -> term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term           -> factor ( ( "-" | "+" ) factor )* ;
factor         -> unary ( ( "/" | "*" ) unary )* ;
unary          -> ( "!" | "-" ) call
               | call ;
call           -> primary ( "(" arguments? ")" )* ;
primary        -> NUMBER | STRING | "true" | "false" | "nil"
               | "(" expression ")" ;

arguments      -> expression ( "," expression )* ;